apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group = GROUP
version = VERSION_NAME

apply from: 'versioning.gradle'

android {
  compileSdkVersion 24

  /*
   * Ubuntu can't run the aapt on 64 bit before installing this packages
   *    sudo apt-get install lib32stdc++6
   *    sudo apt-get install lib32z1
   */

  buildToolsVersion '24.0.2'

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 24
    versionCode buildVersionCode()
    versionName VERSION_NAME
    externalNativeBuild {
      cmake {
        targets "tangram"
        arguments "-DPLATFORM_TARGET=android",
                  "-DANDROID_TOOLCHAIN=clang",
                  "-DANDROID_STL=c++_static",
                  "-DCMAKE_EXPORT_COMPILE_COMMANDS=TRUE"
        cppFlags "-fexceptions",
                 "-frtti"
        abiFilters "armeabi-v7a", "armeabi"
      }
    }
  }

  externalNativeBuild {
    cmake {
      path "../../CMakeLists.txt"
    }
  }

  sourceSets.main {
    manifest.srcFile 'AndroidManifest.xml'
    java.srcDirs = ['src']
  }
}

// Add gdb server to apk
afterEvaluate {
    Sync packageTask = project.getTasks().findByName("packageReleaseJniLibs")
    if (packageTask) { packageTask.include(['**/gdbserver', '**/gdb.setup']) }

    packageTask = project.getTasks().findByName("packageDebugJniLibs")
    if (packageTask) { packageTask.include(['**/gdbserver', '**/gdb.setup']) }
}

dependencies {
  compile 'com.squareup.okhttp:okhttp:2.5.0'
  compile 'xmlpull:xmlpull:1.1.3.1'
  compile 'com.android.support:support-annotations:24.1.1'
}

apply from: file('gradle-mvn-push.gradle')
