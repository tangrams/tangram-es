apply plugin: 'com.android.application'

android {
  compileSdkVersion 26
  buildToolsVersion '25.0.3'

  def apiKey = project.hasProperty('mapzenApiKey') ? mapzenApiKey : System.getenv('MAPZEN_API_KEY')

  assert apiKey != null : "To run the demo, you must provide a Mapzen API key as an environment\
    variable named 'MAPZEN_API_KEY' or a Gradle property named 'mapzenApiKey'"

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 26
    buildConfigField 'String', 'MAPZEN_API_KEY', '"' + apiKey + '"'
  }

  sourceSets.main {
    assets.srcDirs = ['../../../scenes']
  }

  aaptOptions.ignoreAssetsPattern "!*.ttf"

  buildTypes {
    debug {
      debuggable true
      jniDebuggable true
    }
    release {
      minifyEnabled true
    }
  }
}

dependencies {
  compile project(path: ':tangram')
  compile 'com.android.support:design:25.3.1'
}
