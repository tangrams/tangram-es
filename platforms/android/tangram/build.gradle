apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

repositories {
  google()
}

group = GROUP
version = VERSION_NAME

apply from: 'versioning.gradle'

android {
  compileSdkVersion 26
  buildToolsVersion '26.0.2'

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 26
    versionCode buildVersionCode()
    versionName VERSION_NAME
    consumerProguardFiles 'tangram-proguard-rules.txt'
    externalNativeBuild {
      cmake {
        targets 'tangram'
        arguments '-DPLATFORM_TARGET=android',
                  '-DANDROID_TOOLCHAIN=clang',
                  '-DANDROID_STL=c++_shared'
        cppFlags '-std=c++14',
                 '-pedantic',
                 '-fPIC',
                 '-fexceptions',
                 '-frtti',
                 //warnings
                 '-Wall',
                 '-Wignored-qualifiers',
                 '-Wtype-limits',
                 '-Wmissing-field-initializers',
                 '-Wno-format-pedantic'

        abiFilters 'armeabi-v7a'
      }
    }
  }

  externalNativeBuild {
    cmake {
      path '../../../CMakeLists.txt'
    }
  }

  buildTypes {
    debug {
      externalNativeBuild {
        cmake.cppFlags '-g'
      }
    }
    release {
      externalNativeBuild {
        cmake.cppFlags'-g0'
      }
    }
  }
}

dependencies {
  implementation 'com.squareup.okhttp3:okhttp:3.5.0'
  implementation 'com.android.support:support-annotations:26.1.0'
}

apply from: file('gradle-mvn-push.gradle')
