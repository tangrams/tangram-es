apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group = GROUP
version = VERSION_NAME

apply from: 'versioning.gradle'

android {
  compileSdkVersion 26
  buildToolsVersion '26.0.2'

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 26
    versionCode buildVersionCode()
    versionName VERSION_NAME
    consumerProguardFiles 'tangram-proguard-rules.txt'
    externalNativeBuild {
      cmake {
        targets 'tangram'
        arguments '-DPLATFORM_TARGET=android',
                  '-DANDROID_TOOLCHAIN=clang',
                  '-DANDROID_STL=c++_shared'
        cppFlags '-std=c++14',
                 '-pedantic',
                 '-fPIC',
                 '-fexceptions',
                 '-frtti',
                 //warnings
                 '-Wall',
                 '-Wignored-qualifiers',
                 '-Wtype-limits',
                 '-Wmissing-field-initializers',
                 '-Wno-format-pedantic'

        abiFilters 'armeabi-v7a'
      }
    }
  }

  externalNativeBuild {
    cmake {
      path '../../../CMakeLists.txt'
    }
  }

  flavorDimensions "tangramAbi"

  buildTypes {
    debug {
      externalNativeBuild {
        cmake.cppFlags '-g'
      }
    }
    release {
      externalNativeBuild {
        cmake.cppFlags'-g0'
      }
    }
  }
  productFlavors {
    slim {
      // Default configuration
    }
    full {
      externalNativeBuild.cmake.abiFilters 'x86', 'arm64-v8a'
    }
  }

  if (project.hasProperty('doFullRelease')) {
    defaultPublishConfig 'fullRelease'
  } else {
    defaultPublishConfig 'slimDebug'
  }
}

dependencies {
  api 'com.squareup.okhttp3:okhttp:3.5.0'
  api 'com.android.support:support-annotations:25.3.1'
}

apply from: file('gradle-mvn-push.gradle')
