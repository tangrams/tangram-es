apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

ext {
  bintrayRepo = 'maven'
  bintrayName = 'tangram'

  publishedGroupId = 'com.mapzen.tangram'
  libraryName = 'tangram-es'
  artifact = 'tangram-es'

  libraryDescription = '2D and 3D map renderer using OpenGL ES'

  siteUrl = 'https://github.com/tangrams/tangram-es'
  gitUrl = 'https://github.com/tangrams/tangram-es.git'

  libraryVersion = '0.9.1'

  licenseName = 'The MIT License (MIT)'
  licenseUrl = 'https://github.com/tangrams/tangram-es/blob/master/LICENSE'
  allLicenses = ["MIT"]
}


group = GROUP
version = VERSION_NAME

apply from: 'versioning.gradle'

android {
  compileSdkVersion 26

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 26
    versionCode buildVersionCode()
    versionName VERSION_NAME
    consumerProguardFiles 'tangram-proguard-rules.txt'
    externalNativeBuild {
      cmake {
        targets 'tangram'
        arguments '-DPLATFORM_TARGET=android',
                  '-DANDROID_TOOLCHAIN=clang',
                  '-DANDROID_STL=c++_shared'
        cppFlags '-std=c++14',
                 '-pedantic',
                 '-fPIC',
                 '-fexceptions',
                 '-frtti',
                 //warnings
                 '-Wall',
                 '-Wignored-qualifiers',
                 '-Wtype-limits',
                 '-Wmissing-field-initializers',
                 '-Wno-format-pedantic'

        abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'
      }
    }
  }

  externalNativeBuild {
    cmake {
      path '../../../CMakeLists.txt'
    }
  }

  flavorDimensions "tangramAbi"

  buildTypes {
    debug {
      externalNativeBuild {
        cmake.cppFlags '-g'
      }
    }
    release {
      externalNativeBuild {
        cmake.cppFlags'-g0'
      }
    }
  }
}

dependencies {
  api 'com.squareup.okhttp3:okhttp:3.5.0'
  implementation 'com.android.support:support-annotations:25.3.1'
}

apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'
