apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = GROUP
version = VERSION_NAME

apply from: 'versioning.gradle'

android {
  compileSdkVersion 26

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 26
    versionCode buildVersionCode()
    versionName VERSION_NAME
    consumerProguardFiles 'tangram-proguard-rules.txt'
    externalNativeBuild {
      cmake {
        targets 'tangram'
        arguments '-DPLATFORM_TARGET=android',
                  '-DANDROID_TOOLCHAIN=clang',
                  '-DANDROID_STL=c++_shared'
        cppFlags '-std=c++14',
                 '-pedantic',
                 '-fPIC',
                 '-fexceptions',
                 '-frtti',
                 //warnings
                 '-Wall',
                 '-Wignored-qualifiers',
                 '-Wtype-limits',
                 '-Wmissing-field-initializers',
                 '-Wno-format-pedantic'

        abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'
      }
    }
  }

  externalNativeBuild {
    cmake {
      path '../../../CMakeLists.txt'
    }
  }

  buildTypes {
    debug {
      externalNativeBuild {
        cmake.cppFlags '-g'
      }
    }
    release {
      externalNativeBuild {
        cmake.cppFlags'-g0'
      }
    }
  }
}

dependencies {
  api 'com.squareup.okhttp3:okhttp:3.9.1'
  implementation 'com.android.support:support-annotations:26.1.0'
}

// Configure POM for Maven Central.

install {
  repositories.mavenInstaller {
    pom.project {
      packaging POM_PACKAGING
      groupId GROUP
      artifactId POM_ARTIFACT_ID
      name POM_NAME
      description POM_DESCRIPTION
      url POM_URL
      licenses {
        license {
          name POM_LICENCE_NAME
          url POM_LICENCE_URL
        }
      }
      developers {
        developer {
          id POM_DEVELOPER_ID
          name POM_DEVELOPER_NAME
        }
      }
      scm {
        connection POM_SCM_CONNECTION
        developerConnection POM_SCM_DEV_CONNECTION
        url POM_URL
      }
    }
  }
}

// Create tasks for Android artifacts.

task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.srcDirs
}

task androidJavadocs(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
  exclude 'com/almeros/android/multitouch'
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
  classifier = 'javadoc'
  from androidJavadocs.destinationDir
}

artifacts {
  archives androidSourcesJar
  archives androidJavadocsJar
}

// Configure Bintray upload.

bintray {
  user = hasProperty('bintrayUser') ? property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = hasProperty('bintrayApiKey') ? property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
  configurations = ['archives']
  override = true
  pkg {
    userOrg = 'tangrams'
    repo = 'maven'
    name = 'tangram'
    desc = POM_DESCRIPTION
    websiteUrl = POM_URL
    vcsUrl = POM_SCM_URL
    licenses = [POM_LICENCE_NAME]
    publish = true
    publicDownloadNumbers = true
    version {
      name = VERSION_NAME
      gpg {
        sign = true
      }
    }
  }
}
