apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group = GROUP
version = VERSION_NAME

apply from: 'versioning.gradle'

android {
  compileSdkVersion 25
  buildToolsVersion '25.0.0'

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 25
    versionCode buildVersionCode()
    versionName VERSION_NAME
    externalNativeBuild {
      cmake {
        targets 'tangram'
        arguments '-DPLATFORM_TARGET=android',
                  '-DANDROID_TOOLCHAIN=clang',
                  '-DANDROID_STL=c++_shared'
        cppFlags '-std=c++14',
                 '-pedantic',
                 '-fPIC',
                 '-fexceptions',
                 '-frtti',
                 //warnings
                 '-Wall',
                 '-Wignored-qualifiers',
                 '-Wtype-limits',
                 '-Wmissing-field-initializers'

        abiFilters 'armeabi-v7a'
      }
    }
  }

  externalNativeBuild {
    cmake {
      path '../../../CMakeLists.txt'
    }
  }

  buildTypes {
    debug {
      externalNativeBuild {
        cmake.cppFlags '-g'
      }
    }
    release {
      externalNativeBuild {
        cmake.cppFlags'-g0'
      }
    }
  }
  productFlavors {
    slim {
      // Default configuration
    }
    full {
      externalNativeBuild.cmake.abiFilters 'armeabi', 'arm64-v8a', 'x86'
    }
  }

  if (project.hasProperty('doFullRelease')) {
    defaultPublishConfig 'fullRelease'
  } else {
    defaultPublishConfig 'slimDebug'
  }
}

dependencies {
  compile 'com.squareup.okhttp3:okhttp:3.5.0'
  compile 'xmlpull:xmlpull:1.1.3.1'
  compile 'com.android.support:support-annotations:25.0.0'
}

apply from: file('gradle-mvn-push.gradle')
